import org.jetbrains.compose.desktop.application.dsl.TargetFormat

plugins {
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.composeMultiplatform)
    alias(libs.plugins.composeCompiler)
    alias(libs.plugins.kotlinSerialization)
    alias(libs.plugins.nucleus)
}

configurations.all {
    resolutionStrategy {
        force(libs.kotlinx.datetime)
    }
}

kotlin {
    jvm()

    sourceSets {
        @Suppress("DEPRECATION")
        commonMain.dependencies {
            implementation(compose.runtime)
            implementation(compose.foundation)
            implementation(compose.material3)
            implementation(compose.ui)
            implementation(compose.components.resources)
            implementation(libs.kotlinx.coroutines.core)
            implementation(libs.kotlinx.serialization.json)
            implementation(libs.kotlinx.datetime)
            implementation(libs.kermit)
        }
        commonTest.dependencies {
            implementation(libs.kotlin.test)
        }
        jvmTest.dependencies {
            implementation(libs.kotlin.test)
        }
        jvmMain.dependencies {
            implementation(compose.desktop.currentOs)
            implementation(libs.kotlinx.coroutines.swing)
            implementation(libs.ktor.server.core)
            implementation(libs.ktor.server.netty)
            implementation(libs.ktor.server.content.negotiation)
            implementation(libs.ktor.serialization.kotlinx.json)
            implementation(libs.markdown.renderer)
            implementation(libs.markdown.renderer.code)
            implementation(libs.nucleus.decorated.window.core)
            implementation(libs.nucleus.decorated.window.jni)
            implementation(libs.nucleus.decorated.window.material)
        }
    }
}

compose.desktop {
    application {
        mainClass = "com.mikepenz.agentapprover.MainKt"

        nativeDistributions {
            targetFormats(TargetFormat.Dmg, TargetFormat.Msi, TargetFormat.Deb)
            packageName = "AgentApprover"
            packageVersion = "1.0.0"

            macOS {
                iconFile.set(project.file("../icons/app.icns"))
            }
            windows {
                iconFile.set(project.file("../icons/app.ico"))
            }
            linux {
                iconFile.set(project.file("../icons/app.png"))
            }
        }
    }
}
